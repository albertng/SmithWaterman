/* Affine gap penalty
 */

alphabet = {A, C, G, T, N};
const unsigned BITWIDTH = 18;
dpmat signed<BITWIDTH> score[][];
dpmat signed<BITWIDTH> I[][];
dpmat signed<BITWIDTH> D[][];
cell(signed<BITWIDTH> match, signed<BITWIDTH> mismatch,
     signed<BITWIDTH> gap_open, signed<BITWIDTH> gap_extend,
     signed<BITWIDTH> N_penalty) {
  signed<BITWIDTH> substitute;
  if (query_char == N || ref_char == N) {
    substitute = N_penalty;
  } else if (query_char == ref_char) {
    substitute = match;
  } else {
    substitute = mismatch;
  }
  I[row][col] = max(score[row - 1][col] + gap_open,
                    I[row - 1][col] + gap_extend);
  D[row][col] = max(score[row][col - 1] + gap_open,
                    D[row][col - 1] + gap_extend);
  score[row][col] = max(0,
                        score[row - 1][col - 1] + substitute,
                        I[row][col],
                        D[row][col]);
}
